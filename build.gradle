plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'net.ltgt.apt' version '0.10'
}

group 'io.github.swim2sun'
version '1.0.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.json:json:20180130'
    compile 'com.squareup.okhttp3:okhttp:3.11.0'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.11.0'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compileOnly 'org.projectlombok:lombok:1.18.2'
    testCompile 'ch.qos.logback:logback-classic:1.2.3'
    testCompile('org.junit.jupiter:junit-jupiter-api:5.2.0')

    apt "org.projectlombok:lombok:1.18.2"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'weixin-web-sdk'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Weixin Web SDK'
                description = 'Weixin web sdk'
                url = 'https://github.com/swim2sun/weixin-web-sdk'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'swim2sun'
                        name = 'You Xiangyang'
                        email = 'xiangyangyou@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/swim2sun/weixin-web-sdk.git'
                    developerConnection = 'scm:git:https://github.com/swim2sun/weixin-web-sdk.git'
                    url = 'https://github.com/swim2sun/weixin-web-sdk'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username "$ossrhUsername"
                password "$ossrhPassword"
            }
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html4', true)
    }
}